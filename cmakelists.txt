# ------------------------------------------------------------
# Place executables in a dedicated output directory
# ------------------------------------------------------------
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY
    ${CMAKE_BINARY_DIR}/../bin
)

set(CMAKE_LIBRARY_OUTPUT_DIRECTORY
    ${CMAKE_BINARY_DIR}/../bin
)

set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY
    ${CMAKE_BINARY_DIR}/../lib
)

cmake_minimum_required(VERSION 3.10)
project(Voxels)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# ------------------------------------------------------------------
# Compile GLSL shaders directly into C headers using glslangValidator
# ------------------------------------------------------------------
find_program(GLSLANG_VALIDATOR
    NAMES glslangValidator
    HINTS ENV VULKAN_SDK
    PATH_SUFFIXES bin
    REQUIRED
)

file(GLOB_RECURSE GLSL_SRC_FILES
    "${CMAKE_SOURCE_DIR}/src/shaders/glsl/*.comp"
)

set(SHADER_HEADER_DIR ${CMAKE_SOURCE_DIR}/src/shaders)
file(MAKE_DIRECTORY ${SHADER_HEADER_DIR})

set(SHADER_HEADERS)
foreach(GLSL ${GLSL_SRC_FILES})
    get_filename_component(FILE_NAME ${GLSL} NAME_WE)
    set(HDR ${SHADER_HEADER_DIR}/${FILE_NAME}.h)

    # Variable name inside the header
    set(VAR_NAME ${FILE_NAME}_spirv)

    add_custom_command(
        OUTPUT ${HDR}
        COMMAND ${GLSLANG_VALIDATOR}
            -V
            --target-env vulkan1.1
            --vn ${VAR_NAME}
            -o ${HDR}
            ${GLSL}
        DEPENDS ${GLSL}
        COMMENT "Compiling ${GLSL} -> ${HDR}"
        VERBATIM
    )

    list(APPEND SHADER_HEADERS ${HDR})
endforeach()

add_custom_target(Shaders ALL DEPENDS ${SHADER_HEADERS})

# ------------------------------------------------------------------
# Build the program
# ------------------------------------------------------------------
file(GLOB SRC_FILES "${CMAKE_SOURCE_DIR}/src/*.cpp")
add_executable(Voxels ${SRC_FILES})

find_package(Vulkan REQUIRED)

target_include_directories(Voxels PRIVATE
    ${CMAKE_SOURCE_DIR}/include
    ${CMAKE_SOURCE_DIR}/src/shaders
)

target_link_directories(Voxels PRIVATE ${CMAKE_SOURCE_DIR}/lib/SDL3)

target_link_libraries(Voxels
    PRIVATE
    Vulkan::Vulkan
    SDL3
)

target_link_options(Voxels PRIVATE
    -static-libgcc
    -static-libstdc++
)

# Ensure shaders are built first
add_dependencies(Voxels Shaders)
