#version 450
#extension GL_GOOGLE_include_directive : require

#include "voxel.glsl"
#include "ray.glsl"

layout(local_size_x = 16, local_size_y = 16) in;
layout(set=1, binding = 0, rgba8) uniform writeonly image2D destImage;
layout(set=1, binding = 1, r32f) uniform readonly image2D depthImage;

layout(set = 0, binding = 0) buffer WorldInfo {
    vec4 posTime;
    vec2 cameraRot;
} worldInfo;

layout(set = 0, binding = 1) buffer Sizes {
    ivec3 occupancyMin;
    ivec3 occupancyMax;
    int voxels;
    int chunks;
} sizes;

layout(set = 0, binding = 2) buffer Voxels {
    int data[];
} voxel;

layout(set = 0, binding = 3) buffer Chunks {
    Chunk data[];
} chunk;

layout(set = 0, binding = 4) buffer Occupancy {
    ChunkOccupancy data[];
} occupancy;

int getVoxel(int i) {
    int data = voxel.data[i/2];
    data >>= 16*(i&1); // shift high bits if odd index
    return data&0xFFFF; // mask lower bits
}

bool DDAVoxels(Chunk c, Ray ray, out int data) {
    ray.pos -= c.pos * c.size; // bring ray into chunk space
    ray.pos += sign(ray.dir) * 1e-3; // nudge forward

    ivec3 voxel = ivec3(ray.pos);
    vec3 step = sign(ray.dir);

    // branchless tMax computation
    vec3 tMax = ((vec3(voxel) + step * 0.5 + 0.5) - ray.pos) / ray.dir;
    vec3 tDelta = abs(1.0 / ray.dir);

    while (voxel.x >= 0 && voxel.x < c.size.x &&
            voxel.y >= 0 && voxel.y < c.size.y &&
            voxel.z >= 0 && voxel.z < c.size.z) {

        int index = c.index + voxel.x + voxel.y * c.size.x + voxel.z * c.size.x * c.size.y;
        data = getVoxel(index);
        if (isVoxelSolid(data))
            return true;

        // find axis with minimum tMax
        float tMin = min(min(tMax.x, tMax.y), tMax.z);
        // step voxel along min axis
        ivec3 stepMask = ivec3(
            tMax.x == tMin,
            tMax.y == tMin,
            tMax.z == tMin
        );

        voxel += ivec3(step)*stepMask;

        tMax += vec3(
            stepMask.x != 0 ? tDelta.x : 0.0,
            stepMask.y != 0 ? tDelta.y : 0.0,
            stepMask.z != 0 ? tDelta.z : 0.0
        );
    }
    return false;
}

bool DDAChunks(Ray ray, out int data) {
    // move ray to the start of chunk bounds
    ivec3 gridSize = sizes.occupancyMax - sizes.occupancyMin + ivec3(1);
    Box occupancyVolume = createBox(
        sizes.occupancyMin * CHUNKWIDTH,
        gridSize * CHUNKWIDTH
    );

    Intersection occHit = intersectAABB(ray, occupancyVolume);
    if (!occHit.hit)
        return false;

    // start actual dda
    // safe step
    vec3 step = sign(ray.dir);

    float eps = 1e-4;
    // move the ray to the chunk bounds + push it in to avoid floating point error
    vec3 rayStart = ray.pos + ray.dir * occHit.near + eps * step;
    
    const float CHUNK_SIZE = float(CHUNKWIDTH);
    
    // local occupancy coords
    ivec3 chunkPos = ivec3(rayStart / CHUNK_SIZE) - sizes.occupancyMin;
    vec3 tDelta = abs(CHUNK_SIZE / ray.dir);

    // next world boundary
    vec3 nextBoundary = (vec3(chunkPos) +
        vec3(step.x > 0.0 ? 1.0 : 0.0,
             step.y > 0.0 ? 1.0 : 0.0,
             step.z > 0.0 ? 1.0 : 0.0)) * CHUNK_SIZE;

    vec3 tMax = vec3(
        ray.dir.x != 0.0 ? (nextBoundary.x - rayStart.x) / ray.dir.x : 1e30,
        ray.dir.y != 0.0 ? (nextBoundary.y - rayStart.y) / ray.dir.y : 1e30,
        ray.dir.z != 0.0 ? (nextBoundary.z - rayStart.z) / ray.dir.z : 1e30
    );
    float traveled = 0.0;

    while (chunkPos.x >= 0 && chunkPos.x < gridSize.x &&
            chunkPos.y >= 0 && chunkPos.y < gridSize.y &&
            chunkPos.z >= 0 && chunkPos.z < gridSize.z) {

        int occIndex =
            chunkPos.x +
            chunkPos.y * gridSize.x +
            chunkPos.z * gridSize.x * gridSize.y;

        ChunkOccupancy occ = occupancy.data[occIndex];

        if ((occ.flags & CHUNKFLAGS_EMPTY) != 0) {
            Chunk c = chunk.data[occ.index];
            if (DDAVoxels(c,
                Ray(
                    rayStart + ray.dir * traveled,
                    ray.dir),
                data))
                return true;
        }

        float tMin = min(min(tMax.x, tMax.y), tMax.z);

        ivec3 stepMask = ivec3(
            tMax.x == tMin,
            tMax.y == tMin,
            tMax.z == tMin
        );

        chunkPos += stepMask * ivec3(step);
        //chunkWorld += stepMask * ivec3(step);

        traveled = tMin;

        tMax += vec3(
            stepMask.x != 0 ? tDelta.x : 0.0,
            stepMask.y != 0 ? tDelta.y : 0.0,
            stepMask.z != 0 ? tDelta.z : 0.0
        );
    }
    return false;
}

vec2 getUV(ivec2 pos, ivec2 size) {
    float aspectRatio = float(size.x)/size.y;
    vec2 uv = vec2(pos)/size;
    uv *= 2;
    uv -= 1;
    uv.y *= -1;
    uv.x *= aspectRatio;
    return uv;
}

void main() {
    ivec2 pos = ivec2(gl_GlobalInvocationID.xy);
    ivec2 imgSize = imageSize(destImage);
    if (pos.x >= imgSize.x || pos.y >= imgSize.y) return; // dont do pixels that are outside of the texture

    vec2 uv = getUV(pos, imgSize);

    vec3 dir = normalize(vec3(uv, 1.0));

    vec3 camPos = worldInfo.posTime.xyz;
    vec2 camRot = worldInfo.cameraRot;
    float time = worldInfo.posTime.w;

    // Quaternion for pitch
    vec4 qPitch = vec4(vec3(1.0, 0.0, 0.0) * sin(camRot.y*0.5), cos(camRot.y*0.5));

    // Quaternion for yaw
    vec4 qYaw = vec4(vec3(0.0, 1.0, 0.0) * sin(camRot.x*0.5), cos(camRot.x*0.5));

    // Combine into single rotation
    vec4 q = quatMultiply(qYaw, qPitch);
    dir = rotateVectorByQuat(dir, q);

    Ray ray = createRay(
        camPos,
        dir
    );

    float depth = imageLoad(depthImage, pos).r;
    ray.pos += ray.dir*depth;
    vec3 color = ray.dir;
    color = vec3(0.05);

    int data;
    bool hit = DDAChunks(ray, data);
    if (hit) {
        color = getVoxelColor(data);
    }
    //color = color * (depth/60.0);
    imageStore(destImage, pos, vec4(color.bgr, 1.0));
}